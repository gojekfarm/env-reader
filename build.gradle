buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'java-library'
    id 'maven'
    id 'signing'
    id 'com.jfrog.artifactory' version "4.7.1"
}

group 'com.gojek'
version '0.2.6'

dependencies {
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    testImplementation 'junit:junit:4.12'
}

buildscript.repositories.each { repositories.add(it) }

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def ossrhUsername = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : ''
def ossrhPassword = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : ''

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Env Reader'
                packaging 'jar'
                description 'Library to read environment variables for a certain prefix'
                url 'https://github.com/gojekfarm/env-reader'

                scm {
                    url 'https://github.com/gojekfarm/env-reader.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        name 'Data Engineering'
                        email 'de-devs@go-jek.com'
                    }
                }
            }
        }
    }
}

signing {
    sign configurations.archives
}
